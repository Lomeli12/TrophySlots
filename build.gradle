buildscript {
    ext.kotlin_version = '1.0.2'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'

def loadProperties(String sourceFileName) {
    def config = new Properties()
        def propFile = new File(sourceFileName)
            if (propFile.canRead()) {
                config.load(new FileInputStream(propFile))
                    for (Map.Entry property in config) {
                        ext[property.key] = property.value;
                    }
            }
}

loadProperties 'build.properties'

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    // Shading
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

group = "$modGroup"
archivesBaseName = "$modName"
version = "$releaseVersion"

minecraft {
    version = "$mcVersion-$mcfVersion" // grab latest forge
    runDir = "run"
    replace '@VERSION@', project.version
    mappings = "$mcpMap"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "kotlinversion", "$kotlinVersion"

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'kotlinversion':"$kotlinVersion"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
	appendix = "$mcVersion"
    classifier = "universal"
    manifest {
        attributes("FMLAT" : "trophyslots_at.cfg")
    }
}

sourceJar {
    appendix = "$mcVersion"
    classifier = "src"
}

shadowJar {
    appendix = "$mcVersion"
    classifier = "universal"
    relocate 'kotlin', 'net.lomeli.trophyslots.repack.kotlin'
    relocate 'jet.runtime.typeinfo', 'net.lomeli.trophyslots.repack.jet.runtime.typeinfo'
    relocate 'org.jetbrains.annotations', 'net.lomeli.trophyslots.repack.org.jetbrains.annotations'
}

reobf {
    shadowJar { mappingType }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

